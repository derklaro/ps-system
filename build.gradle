/*
 * This file is part of ps-system, licensed under the MIT License (MIT).
 *
 * Copyright (c) 2020-2021 Pasqual K. and contributors
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
  id 'org.cadixdev.licenser' version '0.6.1' apply false
}

defaultTasks 'build', 'test', 'check', 'shadowJar'

allprojects {
  group = 'com.github.derklaro'
  version = "2.0.0-SNAPSHOT-" + getCurrentShortRevision()
}

subprojects {
  apply plugin: 'checkstyle'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'org.cadixdev.licenser'

  repositories {
    mavenLocal()
    mavenCentral()

    maven {
      name = 'spigotmc'
      url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
      name = 'cloudnet-snapshots'
      url = 'https://repo.cloudnetservice.eu/repository/snapshots/'
    }

    maven {
      name = 'cloudnet-releases'
      url = 'https://repo.cloudnetservice.eu/repository/releases/'
    }

    maven {
      name = 'sonatype-releases'
      url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
      name = 'jitpack'
      url = 'https://jitpack.io'
    }
  }

  dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.1'
    // lombok configuration
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
  }

  sourceCompatibility = '8'
  targetCompatibility = '8'

  publishing {
    publications {
      maven(MavenPublication) {
        from(components.java)
      }
    }
  }

  license {
    include '**/*.java'
    header = project.rootProject.file('license_header.txt')
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  tasks.withType(Checkstyle) {
    configFile = project.rootProject.file('checkstyle.xml')
  }
}

def getCurrentShortRevision() {
  new ByteArrayOutputStream().withStream { os ->
    exec {
      executable = "git"
      args = ["rev-parse", "HEAD"]
      standardOutput = os
    }
    return os.toString().trim().substring(0, 8)
  }
}
